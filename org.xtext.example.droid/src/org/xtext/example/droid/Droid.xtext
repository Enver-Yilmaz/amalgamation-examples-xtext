grammar org.xtext.example.droid.Droid with org.eclipse.xtext.common.Terminals

import "http://www.xtext.org/example/droid/Droid"

Application:
	'application' name=STRING
		'=>' packageName=PackageName
		(
			('version:' versionCode=INT '=>' versionName=STRING)?
		  & (sdkVersion=ApplicationUsesSDK)?
		)
	(screens+=Screen | layouts+=Layout | resources+=Resource)+
;

ApplicationUsesSDK:
	'sdk:'
	'{'
	(
		('min:' minSdkVersion=INT ';')?
	  & ('max:' maxSdkVersion=INT ';')?
	  & ('target:' targetSdkVersion=INT ';')?
	)
	'}'
;

Screen:
	'screen' name=ID
	'{'
	(
		('show' layout=[Layout])
	  | widgets=ViewCollection
	)
	'}'
;

Action:
	(GoToURLAction | ShowLayoutAction | InvokeScreenAction)
;

GoToURLAction:
	'goTo' url=STRING
;

ShowLayoutAction:
	'show' layout=[Layout]
;

InvokeScreenAction:
	'invoke' screen=[Screen]
;

ButtonTarget returns InvokeScreenAction:
	'to' screen=[Screen]
;

Layout:
	(isRelative?='relative')?
	'layout' name=ID
	'{'
	(
	  //View Properties
	  ('alpha:' alpha=FLOAT ';')?
	  & ('background:' background=AnyDrawableVA ';')?
	  & ('minHeight:' minHeight=DimensionVA ';')?
	  & ('minWidth:' minWidth=DimensionVA ';')?
	  & ('nextFocusDown:' nextFocusDown=[View] ';')?
	  & ('nextFocusLeft:' nextFocusLeft=[View] ';')?
	  & ('nextFocusRight:' nextFocusRight=[View] ';')?
	  & ('nextFocusUp:' nextFocusUp=[View] ';')?
	  & ('onClick:' onClick=Action ';')?
	  & ('padding:' padding=DimensionVA ';')?
	  & ('paddingBottom:' paddingBottom=DimensionVA';')?
	  & ('paddingLeft:' paddingLeft=DimensionVA';')?
	  & ('paddingRight:' paddingRight=DimensionVA';')?
	  & ('paddingTop:' paddingTop=DimensionVA';')?
	  & ('rotation:' rotation=DimensionVA';')?
	  & ('rotationX:' rotationX=DimensionVA';')?
	  & ('rotationY:' rotationY=DimensionVA';')?
	  & ('saveEnabled:' saveEnabled=BooleanVA';')?
	  & ('scaleX:' scaleX=DimensionVA';')?
	  & ('scaleY:' scaleY=DimensionVA';')?
	  & ('scrollX:' scrollX=DimensionVA';')?
	  & ('scrollY:' scrollY=DimensionVA';')?
	  & ('scrollbars:' scrollbars=BooleanVA';')?
	  & ('transformPivotX:' transformPivotX=DimensionVA';')?
	  & ('transformPivotY:' transformPivotY=DimensionVA';')?
	  & ('translationX:' translationX=DimensionVA';')?
	  & ('translationY:' translationY=DimensionVA';')?
	  & ('visibility:' visibility=LayoutVisibilityKind';')?

	  & (layoutProperties=LayoutProperties)?

	  //Layout Properties
	  & ('gravity:' gravity+=LayoutGravityKind ('|' gravity+=LayoutGravityKind)* ';')?
	  & ('orientation:' orientation=LayoutOrientationKind ';')?
	)

	views=ViewCollection
	'}'
;

ViewCollection:
	('#' views+=View)+
;

View:
	Widget | Layout
;

Widget:
	TextView | Button | ImageView | EditText | Spinner
;

TextView:
	'textView' (name=ID)?
		text=StringVA
	'{'
	(
		(layoutProperties=LayoutProperties)?
	  & ('top:' top=DimensionVA ';')?
	  & ('left:' left=DimensionVA ';')?
	  & ('width:' width=DimensionVA ';')?
	  & ('height:' height=DimensionVA ';')?
	  & ('background:' background=AnyDrawableVA ';')?
	  & ('clickable:' clickable=BooleanVA ';')?
	  & ('fadeScrollBars:' fadeScrollBars=BooleanVA ';')?
	  & ('isScrollContainer:' isScrollContainer=BooleanVA ';')?

	  & ('onClick:' onClick=Action ';')?

	  //TextView attributes
	  & ('autoLink:' autoLink=AutoLinkKind ';')?
	  & ('autoText:' autoText=BooleanVA ';')?
	  & ('capitalize:' capitalize=CapitalizeKind ';')?
	  & ('digits:' digits=StringVA ';')?
	  & ('editable:' editable=BooleanVA ';')?
	  & ('gravity:' gravity=LayoutGravityKind';')?
	  & ('hint:' hint=StringVA ';')?
	  & ('numeric:' numeric=BooleanVA ';')?
	  & ('password:' password=BooleanVA ';')?
	  & ('phoneNumber:' phoneNumber=BooleanVA ';')?
	  & ('singleLine:' singleLine=BooleanVA ';')?
	  & ('textColor:' textColor=ColorVA ';')?
	  & ('typeface:' typeface=TypefaceKind ';')?
	  & ('textSize:' textSize=DimensionVA ';')?
	  & ('textStyle:' textStyle+=TextStyleKind ('|' textStyle+=TextStyleKind)* ';')?		 
	)
	'}'
;

Button:
	'button' (name=ID)?
		( text=StringVA | src=AnyDrawableVA )
		( onClick=ButtonTarget )?
	'{'
	(
		(layoutProperties=LayoutProperties)?
	  & ('top:' top=DimensionVA ';')?
	  & ('left:' left=DimensionVA ';')?
	  & ('width:' width=DimensionVA ';')?
	  & ('height:' height=DimensionVA ';')?
	  & ('background:' background=AnyDrawableVA ';')?
	  & ('clickable:' clickable=BooleanVA ';')?

	  & ('onClick:' onClick=Action ';')?

	  // Specific properties
	  & ('hint:' hint=StringVA ';')?
	)
	'}'
;

ImageView:
	'imageView' (name=ID)?
		src=AnyDrawableVA
	'{'
	(
		(layoutProperties=LayoutProperties)?
	  & ('top:' top=DimensionVA ';')?
	  & ('left:' left=DimensionVA ';')?
	  & ('width:' width=DimensionVA ';')?
	  & ('height:' height=DimensionVA ';')?
	  & ('background:' background=AnyDrawableVA ';')?
	  & ('clickable:' clickable=BooleanVA ';')?
	  & ('fadeScrollBars:' fadeScrollBars=BooleanVA ';')?
	  & ('isScrollContainer:' isScrollContainer=BooleanVA ';')?

	  & ('onClick:' onClick=Action ';')?
	)
	'}'
;

Spinner:
	'spinner' (name=ID)?
		prompt=StringVA
	'{'
	(
		(layoutProperties=LayoutProperties)?
	  & ('top:' top=DimensionVA ';')?
	  & ('left:' left=DimensionVA ';')?
	  & ('width:' width=DimensionVA ';')?
	  & ('height:' height=DimensionVA ';')?
	  & ('background:' background=AnyDrawableVA ';')?
	  & ('clickable:' clickable=BooleanVA ';')?
	  & ('fadeScrollBars:' fadeScrollBars=BooleanVA ';')?
	  & ('isScrollContainer:' isScrollContainer=BooleanVA ';')?

	  & ('onClick:' onClick=Action ';')?
	)
	'}'
;

EditText:
	'editText' (name=ID)?
		text=StringVA?
	'{'
	(
		(layoutProperties=LayoutProperties)?
	  & ('top:' top=DimensionVA ';')?
	  & ('left:' left=DimensionVA ';')?
	  & ('width:' width=DimensionVA ';')?
	  & ('height:' height=DimensionVA ';')?
	  & ('background:' background=AnyDrawableVA ';')?
	  & ('clickable:' clickable=BooleanVA ';')?
	  & ('fadeScrollBars:' fadeScrollBars=BooleanVA ';')?
	  & ('isScrollContainer:' isScrollContainer=BooleanVA ';')?

	  & ('onClick:' onClick=Action ';')?

	  //TextView attributes
	  & ('autoLink:' autoLink=AutoLinkKind ';')?
	  & ('autoText:' autoText=BooleanVA ';')?
	  & ('capitalize:' capitalize=CapitalizeKind ';')?
	  & ('digits:' digits=StringVA ';')?
	  & ('editable:' editable=BooleanVA ';')?
	  & ('gravity:' gravity=LayoutGravityKind ';')?
	  & ('hint:' hint=StringVA ';')?
	  & ('numeric:' numeric=BooleanVA ';')?
	  & ('password:' password=BooleanVA ';')?
	  & ('phoneNumber:' phoneNumber=BooleanVA ';')?
	  & ('singleLine:' singleLine=BooleanVA ';')?
	  & ('textColor:' textColor=ColorVA';')?
	  & ('typeface:' typeface=TypefaceKind ';')?
	  & ('textSize:' textSize=DimensionVA ';')?
	  & ('textStyle:' textStyle+=TextStyleKind ('|' textStyle+=TextStyleKind)* ';')?
	)
	'}'
;

Resource:
	StringResource | IntegerResource | BooleanResource
	| ColorResource | DimensionResource
	| ArrayResource | DrawableResource
;

StringResource:
	name=ID '=' value=STRING
;

IntegerResource:
	name=ID '=' value=INT
;

BooleanResource:
	name=ID '=' value=BOOL
;

ColorResource:
	name=ID '=' value=HEX_COLOR
;

DimensionResource:
	name=ID '=' value=DimensionValue
;

ArrayResource:
	IntegerArrayResource | StringArrayResource
;

IntegerArrayResource:
	name=ID '=' '['
		(items+=INT (',' items+=INT)* )
	']'
;

StringArrayResource:
	name=ID '=' '['
		(items+=STRING (',' items+=STRING)* )
	']'
;

DrawableResource:
	(BitmapDrawableResource | TransitionDrawableResource)
;

BitmapDrawableResource:
	name=ID '=' filename=ID
;

TransitionDrawableResource:
	name=ID
		from=[BitmapDrawableResource] '<->' to=[BitmapDrawableResource]
;

ValueAccess:
	(StringVA | IntegerVA | BooleanVA | AnyDrawableVA | DimensionVA | LayoutDimensionVA)
	| ResourceAccess
;

AnyDrawableVA:
	DrawableVA | ColorVA
;

StringVA:
	StringRA | value=STRING
;

IntegerVA:
	IntegerRA | value=INT
;

BooleanVA:
	BooleanRA | value=BOOL
;

ColorVA:
	ColorRA | value=HEX_COLOR
;

DimensionVA:
	DimensionRA | value=DimensionValue
;

LayoutDimensionVA:
	DimensionVA | (const_value=LayoutDimensionKind)
;

DrawableVA:
	DrawableRA
;

ResourceAccess:
	StringRA | IntegerRA | BooleanRA | ColorRA | DimensionRA | DrawableRA
;

StringRA:
	'string'
	(
	  ( '(' (package=PackageName '/')? resource=[StringResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

IntegerRA:
	'integer'
	(
	  ( '(' (package=PackageName '/')? resource=[IntegerResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

BooleanRA:
	'bool'
	(
	  ( '(' (package=PackageName '/')? resource=[BooleanResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

ColorRA:
	'color'
	(
	  ( '(' (package=PackageName '/')? resource=[ColorResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

DimensionRA:
	'dimen'
	(
	  ( '(' (package=PackageName '/')? resource=[DimensionResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

ArrayRA:
	'array'
	(
	  ( '(' (package=PackageName '/')? resource=[ArrayResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

DrawableRA:
	'drawable'
	(
	  ( '(' (package=PackageName '/')? resource=[DrawableResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

LayoutProperties:
	'layout:'
	'{'
	 (
		//LinearLayoutParams
		('height:' layout_height=LayoutDimensionVA ';')?
		& ('width:' layout_width=LayoutDimensionVA ';')?
		& ('weight:' layout_weight=IntegerVA ';')?
		& ('marginBottom:' layout_marginBottom=DimensionVA ';')?
		& ('marginLeft:' layout_marginLeft=DimensionVA ';')?
		& ('marginRight:' layout_marginRight=DimensionVA ';')?
		& ('marginTop:' layout_marginTop=DimensionVA ';')?

		//RelativeLayoutParams
		& ('above:' layout_above=[View] ';')?
		& ('alignBaseline:' layout_alignBaseline=[View] ';')?
		& ('alignBottom:' layout_alignBottom=[View] ';')?
		& ('alignLeft:' layout_alignLeft=[View] ';')?
		& ('alignParentBottom:' layout_alignParentBottom=BooleanVA ';')?
		& ('alignParentLeft:' layout_alignParentLeft=BooleanVA ';')?
		& ('alignParentRight:' layout_alignParentRight=BooleanVA ';')?
		& ('alignParentTop:' layout_alignParentTop=BooleanVA ';')?
		& ('alignTop:' layout_alignTop=[View] ';')?
		& ('alignWithParentIfMissing:' layout_alignWithParentIfMissing=BooleanVA ';')?
		& ('below:' layout_below=[View] ';')?
		& ('centerHorizontal:' layout_centerHorizontal=BooleanVA ';')?
		& ('centerInParent:' layout_centerInParent=BooleanVA ';')?
		& ('centerVertical:' layout_centerVertical=BooleanVA ';')?
		& ('toLeftOf:' layout_toLeftOf=[View] ';')?
		& ('toRightOf:' layout_toRightOf=[View] ';')?
	)
	'}'
;

PackageName:
	ID ('.' ID)*
;

DimensionValue:
	FLOAT ('dp' | 'sp' | 'pt' | 'px' | 'mm' | 'in')
;

BOOL:
	'YES' | 'NO'
;

FLOAT:
	INT ('.' INT)?
;

enum LayoutDimensionKind:
	fill_parent | match_parent | wrap_content
;

enum LayoutVisibilityKind:
	visible | invisible | gone
;

enum LayoutGravityKind :
	top | bottom | left | right |
	center | center_vertical | center_horizontal |
	fill | fill_vertical | fill_horizontal |
	clip_vertical | clip_horizontal
;

enum LayoutOrientationKind:
	horizontal | vertical
;

enum AutoLinkKind:
	none | web | email | phone | map | all
;

enum CapitalizeKind:
	none | sentences | words | characters
;

enum TypefaceKind:
	normal | sans | serif | monospace
;

enum TextStyleKind:
	normal | bold | italic
;

terminal HEX_COLOR:
	'#'
	('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f')
	('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f')
	('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f')
	(('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f'))?
;
grammar org.xtext.example.droid.Droid with org.eclipse.xtext.common.Terminals

generate droid "http://www.xtext.org/example/droid/Droid"

Application:
	'application' name=STRING
		'=>' packageName=PackageName
	(screens+=Screen | layouts+=Layout | resources+=Resource)+
;

Screen:
	'screen' name=ID
	'{'
		('show' layout=[Layout])?
	'}'
;

Layout:
	'layout' name=ID
	'{'
		(layoutProperties=LayoutProperties)?
		views=ViewCollection
	'}'
;

ViewCollection:
	('#' views+=View)+
;

View:
	Widget | Layout
;

Widget:
	TextView | Button | ImageView | EditText | Spinner
;

TextView:
	'textView' (name=ID)?
		text=StringVA
	'{'
		(layoutProperties=LayoutProperties)?
	'}'
;

Button:
	'button' (name=ID)?
		( text=StringVA | src=AnyDrawableVA )
	'{'
		(layoutProperties=LayoutProperties)?
	'}'
;

ImageView:
	'imageView' (name=ID)?
		src=AnyDrawableVA
	'{'
		(layoutProperties=LayoutProperties)?
	'}'
;

Spinner:
	'spinner' (name=ID)?
		prompt=StringVA
	'{'
		(layoutProperties=LayoutProperties)?
	'}'
;

EditText:
	'editText' (name=ID)?
		text=StringVA?
	'{'
		(layoutProperties=LayoutProperties)?
	'}'
;

Resource:
	StringResource | IntegerResource | BooleanResource
	| ColorResource | DimensionResource
	| ArrayResource | DrawableResource
;

StringResource:
	name=ID '=' value=STRING
;

IntegerResource:
	name=ID '=' value=INT
;

BooleanResource:
	name=ID '=' value=BOOL
;

ColorResource:
	name=ID '=' value=HEX_COLOR
;

DimensionResource:
	name=ID '=' value=DimensionValue
;

ArrayResource:
	IntegerArrayResource | StringArrayResource
;

IntegerArrayResource:
	name=ID '=' '['
		(items+=INT (',' items+=INT)* )
	']'
;

StringArrayResource:
	name=ID '=' '['
		(items+=STRING (',' items+=STRING)* )
	']'
;

DrawableResource:
	(BitmapDrawableResource | TransitionDrawableResource)
;

BitmapDrawableResource:
	name=ID '=' filename=ID
;

TransitionDrawableResource:
	name=ID
		from=[BitmapDrawableResource] '<->' to=[BitmapDrawableResource]
;

ValueAccess:
	(StringVA | IntegerVA | BooleanVA | AnyDrawableVA | DimensionVA)
	| ResourceAccess
;

AnyDrawableVA:
	DrawableVA | ColorVA
;

StringVA:
	StringRA | value=STRING
;

IntegerVA:
	IntegerRA | value=INT
;

BooleanVA:
	BooleanRA | value=BOOL
;

ColorVA:
	ColorRA | value=HEX_COLOR
;

DimensionVA:
	DimensionRA | value=DimensionValue
;

DrawableVA:
	DrawableRA
;

ResourceAccess:
	StringRA | IntegerRA | BooleanRA | ColorRA | DimensionRA | DrawableRA
;

StringRA:
	'string'
	(
	  ( '(' (package=PackageName '/')? resource=[StringResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

IntegerRA:
	'integer'
	(
	  ( '(' (package=PackageName '/')? resource=[IntegerResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

BooleanRA:
	'bool'
	(
	  ( '(' (package=PackageName '/')? resource=[BooleanResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

ColorRA:
	'color'
	(
	  ( '(' (package=PackageName '/')? resource=[ColorResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

DimensionRA:
	'dimen'
	(
	  ( '(' (package=PackageName '/')? resource=[DimensionResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

ArrayRA:
	'array'
	(
	  ( '(' (package=PackageName '/')? resource=[ArrayResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

DrawableRA:
	'drawable'
	(
	  ( '(' (package=PackageName '/')? resource=[DrawableResource] ')' )
	| ( '[' (package=PackageName '/')? externalResource=ID ']' )
	)
;

LayoutProperties:
	'layout:'
	'{'
		//LinearLayoutParams
		('height:' layout_height=DimensionVA ';')?
		('width:' layout_width=DimensionVA ';')?
		('weight:' layout_weight=IntegerVA ';')?
		('marginBottom:' layout_marginBottom=DimensionVA ';')?
		('marginLeft:' layout_marginLeft=DimensionVA ';')?
		('marginRight:' layout_marginRight=DimensionVA ';')?
		('marginTop:' layout_marginTop=DimensionVA ';')?

		//RelativeLayoutParams
		('above:' layout_above=[View] ';')?
		('alignBaseline:' layout_alignBaseline=[View] ';')?
		('alignBottom:' layout_alignBottom=[View] ';')?
		('alignLeft:' layout_alignLeft=[View] ';')?
		('alignParentBottom:' layout_alignParentBottom=BooleanVA ';')?
		('alignParentLeft:' layout_alignParentLeft=BooleanVA ';')?
		('alignParentRight:' layout_alignParentRight=BooleanVA ';')?
		('alignParentTop:' layout_alignParentTop=BooleanVA ';')?
		('alignTop:' layout_alignTop=[View] ';')?
		('alignWithParentIfMissing:' layout_alignWithParentIfMissing=BooleanVA ';')?
		('below:' layout_below=[View] ';')?
		('centerHorizontal:' layout_centerHorizontal=BooleanVA ';')?
		('centerInParent:' layout_centerInParent=BooleanVA ';')?
		('centerVertical:' layout_centerVertical=BooleanVA ';')?
		('toLeftOf:' layout_toLeftOf=[View] ';')?
		('toRightOf:' layout_toRightOf=[View] ';')?
	'}'
;

PackageName:
	ID ('.' ID)*
;

DimensionValue:
	FLOAT ('dp' | 'sp' | 'pt' | 'px' | 'mm' | 'in')
;

BOOL:
	'YES' | 'NO'
;

FLOAT:
	INT ('.' INT)?
;

terminal HEX_COLOR:
	'#'
	('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f')
	('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f')
	('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f')
	(('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f'))?
;